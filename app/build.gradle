import java.text.SimpleDateFormat

apply plugin: "com.android.application"
apply plugin: "kotlin-android"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.ykrc17.shortcutcreator"
        minSdkVersion 18
        targetSdkVersion 28
        versionName new SimpleDateFormat("yyMMdd").format(new Date())
        versionCode versionName.toInteger()
        resConfigs "xxhdpi", "ldltr", "appliance"
    }
    signingConfigs {
        release {//发布版本的签名配置
            storeFile file("keystore.jks")
            keyAlias "key0"
            storePassword "123456"
            keyPassword "123456"
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
}

android.sourceSets.main {
    java.srcDirs = ["src/java"]
    res.srcDirs = ["src/res"]
    manifest.srcFile "src/AndroidManifest.xml"
    assets.srcDirs = ["src/assets"]
}

dependencies {
    def supportVersion = "28.0.0"
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation([
            "com.android.support:appcompat-v7:$supportVersion",
            "com.android.support:recyclerview-v7:$supportVersion",
            "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
    ])
}


tasks.whenTaskAdded {
    if (it.name == "packageRelease") {
        it.doLast {
            copy {
                from("$buildDir/outputs/apk/release") {
                    include "*.apk"
                    rename("app-release", "shortcut-creator-${android.defaultConfig.versionName}")
                }
                into rootProject.file("release")
            }
        }
    }
}